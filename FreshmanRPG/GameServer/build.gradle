plugins {
    id 'java'
    id 'checkstyle'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id "org.springframework.boot" version "2.7.5"
}

apply from: rootProject.file("dependencies.gradle")
apply from: 'database-builders.gradle'

mainClassName = "edu.ship.engr.shipsim.Server"

group 'edu.ship.cs'
version '1.0-SNAPSHOT'


//java {
//    sourceCompatibility = JavaVersion.VERSION_17
//    targetCompatibility = JavaVersion.VERSION_17
//}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':GameShared')
    testImplementation project(path: ':GameShared', configuration: 'testArtifacts')

    normDependencies.values().forEach {
        implementation it
    }

    testDependencies.values().forEach {
        testImplementation it
    }

    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.7.5'
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.7.5'
}

configurations {
    testArtifacts.extendsFrom testRuntime
}
task testJar(type: Jar) {
    classifier "test"
    from sourceSets.test.output
}
artifacts {
    testArtifacts testJar
}

checkstyle {
    ignoreFailures = false
    maxWarnings = 0
}

shadowJar {
    dependsOn(test)
    archiveFileName = 'GameServer.jar'
    destinationDirectory = new File(project.projectDir, "build")
}

tasks.named('processResources') {
    expand(project.properties)
}

test {
    useJUnitPlatform()
}